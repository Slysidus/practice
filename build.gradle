plugins {
    id 'java'
    id 'io.freefair.lombok' version '5.3.3.3'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'com.oxymore'
version '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()

    maven { url = 'https://maven.enginehub.org/repo' }
}

dependencies {
    implementation group: 'org.mongodb', name: 'bson', version: '4.2.3'
    implementation group: 'org.mongodb', name: 'mongodb-driver', version: '3.12.8'

    // manually installed jar in local repo
    // (see docs for `mvn install`)
    compileOnly 'com.oxymore:server:1.7.10-R4-SNAPSHOT'

    compileOnly('com.sk89q.worldedit:worldedit-bukkit:6.1.4-SNAPSHOT') {
        exclude group: 'org.bukkit', module: 'bukkit'
    }
}

processResources {
    filesMatching('**/**.yml') {
        filter {
            it.replace('${version}', project.version)
        }
    }
}

// shadowing
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "com.oxymore.practice.libs"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

shadowJar {
}